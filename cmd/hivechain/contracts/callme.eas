;;; -*- mode: asm -*-
;;; This contract returns value `theOutput` if calldata is exactly equal to `theInput`.
;;; Otherwise, reverts with an error message.

#define theInput 0xff01
#define theOutput 0xffee

#define %revert(error) {        ; []
    push $error                 ; [value]
    push 0                      ; [offset, value]
    mstore                      ; []
    push .bytelen($error)       ; [size]
    push 32-.bytelen($error)    ; [offset, size]
    revert
}

.start:
    calldatasize                ; [size]
    push .bytelen(theInput)     ; [exp, iszero]
    eq                          ; [size==exp]
    jumpi @compare              ; []
    %revert("wrong-calldatasize")

compare:
    push 0                      ; [offset]
    calldataload                ; [calldata]
    push 256-.bitlen(theInput)  ; [shft, calldata]
    shr                         ; [data]
    push theInput               ; [expv, data]
    eq                          ; [data==expv]
    jumpi @return               ; []
    %revert("wrong-calldata")

return:
    push theOutput              ; [v]
    push 0                      ; [offset, v]
    mstore                      ; []
    push .bytelen(theOutput)    ; [size]
    push 32-.bytelen(theInput)  ; [offset, size]
    return                      ; []
